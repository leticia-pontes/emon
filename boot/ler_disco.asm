; FUNÇÃO PARA LEITURA D "N" SETORES DE UMA UNIDADE DE ARMAZENAMENTO.
; A QUANTIDADE DE SETORES A SER LIDA DEVERÁ SER COLOCADA ANTES DE CHAMAR A FUNÇÃO NO REGISTRADOR DH.
; O NÚMERO DA UNIDADE DE ARMAZENAMENTO A SER LIDA DEVERÁ TER SIDO COLOCADO EM DL. 
; OS SETORES LIDOS FICARÃO DISPONÍVEIS NA MEMÓRIA NO ENDEREÇO ES:BX

LER_DISCO:
    PUSHA
    PUSH DX 
    MOV AH, 0x02 ; FUNÇÃO 2 DA BIOS (LEITURA)
    MOV AL, DH ; COPIAMOS PARA AL A QUANTIDADE DE SETORES A SER LIDA
    MOV CL, 0x02 ; NÚMERO DO SETOR A SER LIDO (0x01 É O BOOT)
    MOV CH, 0x00 ; NÚMERO DO CILINDRO (0x00)
    ; DL JÁ DEVE CONTER O CÓDIGO DA UNIDADE, SENDO:
    ; 0: DISQUETE A   1: DISQUETE B   0x80: HD1 (C:)  0x81: HD2 (D:)
    MOV DH, 0x00 ; NÚMERO DA CABEÇA [ES:BX] É O LOCAL DA MEMÓRIA ONDE FICARÃO OS DADOS LIDOS
    INT 0x13 ; INTERRUPÇÃO 13H: SERVIÇOS DE DISCO DA BIOS
    JC ERRO_DISCO ; PULE PARA ERRO_DISCO SE GEROU UM "CARRY BIT"

    POP DX
    CMP AL, DH ; VERIFICA SE A QUANTIDADE DE SETORES LIDA ESTÁ
    JNE ERRO_SETOR ; CORRETA. SE NÃO (JUMP NOT EQUAL) PULA PARA TRATAMENTO DO ERRO. SE NÃO, ENCERRA
    POPA
    RET

%INCLUDE "imprime_hexa.asm"

ERRO_DISCO:
    MOV BX, MENSAGEM_ERRO_DISCO
    CALL IMPRIME
    CALL IMPRIME_QUEBRA
    MOV DH, AH ; AH CONTÉM O CÓDIGO DO ERRO
    CALL IMPRIME_HEXA
    JMP LOOP_DISCO

ERRO_SETOR:
    MOV BX, MENSAGEM_ERRO_SETOR
    CALL IMPRIME

LOOP_DISCO:
    JMP $

MENSAGEM_ERRO_DISCO: DB "ERRO NA LEITURA DO DISCO", 0
MENSAGEM_ERRO_SETOR: DB "QTD. INCORRETA DE SETORES LIDOS", 0
